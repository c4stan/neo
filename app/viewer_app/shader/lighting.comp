#version 450

#include "common.glsl"
#include "lighting_common.glsl"

#extension GL_EXT_debug_printf : enable

layout ( binding = 0, set = xs_shader_binding_set_dispatch_m ) uniform uniform_buffer_t {
    uvec3 grid_size;
    float z_scale;
    float z_bias;
    uint shadow_size;
} uniform_buffer;

layout ( binding = 1, set = xs_shader_binding_set_dispatch_m ) buffer readonly light_buffer_t {
    uint light_count;
    uint _pad0[3];
    light_t lights[];
} light_buffer;

layout ( binding = 2, set = xs_shader_binding_set_dispatch_m ) buffer readonly light_list_buffer_t {
    uint global_offset_accumulator;
    uint list[];
} light_list_buffer;

layout ( binding = 3, set = xs_shader_binding_set_dispatch_m ) buffer readonly light_grid_buffer_t {
    light_grid_t grid[];
} light_grid_buffer;

layout ( binding = 4, set = xs_shader_binding_set_dispatch_m ) uniform texture2D tex_color;
layout ( binding = 5, set = xs_shader_binding_set_dispatch_m ) uniform texture2D tex_normal;
layout ( binding = 6, set = xs_shader_binding_set_dispatch_m ) uniform texture2D tex_material;
//layout ( binding = 7, set = xs_shader_binding_set_dispatch_m ) uniform texture2D tex_radiosity;
layout ( binding = 7, set = xs_shader_binding_set_dispatch_m ) uniform texture2D tex_depth;
layout ( binding = 8, set = xs_shader_binding_set_dispatch_m ) uniform texture2D tex_shadows;

layout ( binding = 9, set = xs_shader_binding_set_dispatch_m, r11f_g11f_b10f ) uniform writeonly image2D img_color;

layout ( binding = 10, set = xs_shader_binding_set_dispatch_m ) uniform sampler sampler_linear;

layout ( local_size_x = 8, local_size_y = 8, local_size_z = 1 ) in;

float shadow_gather ( vec2 uv, float z ) {
#if 1
    vec4 depths = textureGather ( sampler2D ( tex_shadows, sampler_linear ), uv );
    vec4 compare = vec4 ( z );
    vec4 test = step ( depths, compare );
    return dot ( test, vec4 ( 1 ) );
#else
    float shadow_depth = texture ( sampler2D ( tex_shadows, sampler_linear ), uv ).x;
    float shadow_contribution = z > shadow_depth ? 1.f : 0.f;
    return shadow_contribution;
#endif
}

// fresnel (F)
vec3 ggx_f ( float cos_theta, vec3 f0 ) {
    return f0 + ( vec3 ( 1.0 ) - f0 ) * pow ( 1.0 - cos_theta, 5 );
}

// normal distribution function (D)
float ggx_ndf ( float nh, float roughness ) {
    float a2 = roughness * roughness;
    float f = ( nh * a2 - nh ) * nh + 1.0;
    return a2 / ( PI * f * f );
}

// visibility (V) - includes geometric shadowing (G)
float ggx_v ( float nv, float nl, float roughness ) {
    float a = roughness;
    float ggx_v = nl * ( nv * ( 1.0 - a ) + a );
    float ggx_l = nv * ( nl * ( 1.0 - a ) + a );
    return 0.5 / ( ggx_v + ggx_l );
}

vec3 brdf ( vec3 l, vec3 n, vec3 v, vec3 base_color, float roughness, float metalness ) {
    vec3 h = normalize ( v + l );
    float nv = abs ( dot ( n, v ) ) + 1e-5;
    float nl = clamp ( dot ( n, l ), 0, 1 );
    float nh = clamp ( dot ( n, h ), 0, 1 );
    float lh = clamp ( dot ( l, h ), 0, 1 );

    roughness = clamp ( roughness, 0.045, 1 );
    vec3 f0 = mix ( vec3 ( 0.04 ), base_color, metalness );
    
    // spec
    float d = ggx_ndf ( nh, roughness );
    vec3 f = ggx_f ( lh, f0 );
    float g = ggx_v ( nv, nl, roughness );
    vec3 fr = d * g * f;

    // diff
    vec3 ks = f;
    vec3 kd = ( vec3 ( 1.0 ) - ks ) * ( 1 - metalness );
    vec3 fd = kd * base_color / PI;

    return fr + fd;
}

// Karis Sig13
float light_falloff ( float distance, float radius ) {
    float x = distance / radius;
    float y = clamp ( 1 - ( x * x * x * x ), 0, 1 );
    y *= y;
    return y / ( distance * distance + 1 );
}

void main ( void ) {
    vec2 screen_uv = vec2 ( ( gl_GlobalInvocationID.xy + vec2 ( 0.5 ) ) / frame_uniforms.resolution_f32 );

    vec4 normal_rough_sample = texture ( sampler2D ( tex_normal, sampler_linear ), screen_uv );
    vec3 view_normal = normalize ( normal_rough_sample.xyz * 2 - 1 );
    float roughness = normal_rough_sample.w;
    vec4 color_metal_sample = texture ( sampler2D ( tex_color, sampler_linear ), screen_uv );
    vec3 base_color = color_metal_sample.xyz;
    float metalness = color_metal_sample.w;
    //vec3 radiosity = texture ( sampler2D ( tex_radiosity, sampler_linear ), screen_uv ).xyz;
    float depth = texture ( sampler2D ( tex_depth, sampler_linear ), screen_uv ).x;

    vec3 view_geo_pos = view_from_depth ( screen_uv, depth );
    // Normal Offset shadows, helps with acne
    // https://digitalrune.github.io/DigitalRune-Documentation/html/3f4d959e-9c98-4a97-8d85-7a73c26145d7.htm
    // https://mynameismjp.wordpress.com/2013/09/10/shadow-maps/
    // https://c0de517e.blogspot.com/2011/05/shadowmap-bias-notes.html
    // https://ndotl.wordpress.com/2014/12/19/notes-on-shadow-bias/
    view_geo_pos += view_normal * 0.03;
    vec4 world_geo_pos = frame_uniforms.world_from_view * vec4 ( view_geo_pos, 1.f );

    vec3 irradiance = vec3 ( 0, 0, 0 );

    uvec3 cluster_3d_idx;
    // TODO precompute
    //cluster_3d_idx.z = uint ( max ( log2 ( linearize_depth ( depth ) ) * uniform_buffer.z_scale + uniform_buffer.z_bias, 0 ) );
    cluster_3d_idx.z = uint ( max ( log ( linearize_depth ( depth ) ) 
        * ( uniform_buffer.grid_size.z / log ( frame_uniforms.z_far / frame_uniforms.z_near ) )
        + ( - ( uniform_buffer.grid_size.z * log ( frame_uniforms.z_near ) ) / log ( frame_uniforms.z_far / frame_uniforms.z_near ) )
        , 0 ) );
    cluster_3d_idx.xy = uvec2 ( ( gl_GlobalInvocationID.xy + vec2 ( 0.5 ) ) / frame_uniforms.resolution_f32 * uniform_buffer.grid_size.xy );

    uint cluster_idx = cluster_3d_idx.x + uniform_buffer.grid_size.x * cluster_3d_idx.y + ( uniform_buffer.grid_size.x * uniform_buffer.grid_size.y ) * cluster_3d_idx.z;

    light_grid_t grid = light_grid_buffer.grid[cluster_idx];
    for ( uint i = 0; i < grid.count; ++i ) {
        uint list_idx = grid.offset + i;
        uint light_idx = light_list_buffer.list[list_idx];
        light_t light = light_buffer.lights[light_idx];

        vec3 world_light_pos = light.pos;
        float light_emissive = light.emissive;
        vec3 light_color = light.color;

        vec4 shadow_view = light.view_from_world * world_geo_pos;
        vec3 shadow_screen = screen_from_view ( light.proj_from_view, shadow_view.xyz );
        vec2 shadow_texel = light.shadow_tile.xy + shadow_screen.xy * light.shadow_tile.z;

        float shadow = 0;

        if ( any ( greaterThan ( shadow_screen, vec3(1.0) ) ) || any ( lessThan ( shadow_screen, vec3(0.0) ) ) ) {
            continue;
        }

        float texel_size = ( 1.f / uniform_buffer.shadow_size );

#if 0
        int pcf_radius = 8;
        for ( int dx = -pcf_radius; dx <= pcf_radius; dx += 1 ) {
            for ( int dy = -pcf_radius; dy <= pcf_radius; dy += 1 ) {
                vec2 shadow_uv = shadow_texel + vec2 ( dx, dy );
                //shadow_uv = shadow_uv - fract ( shadow_uv );
                //shadow_uv += vec2 ( 0.5 );
                shadow_uv = clamp ( shadow_uv, light.shadow_tile.xy + vec2 ( 0.5 ), light.shadow_tile.xy + vec2 ( light.shadow_tile.z, light.shadow_tile.z ) - vec2 ( 0.5 ) );
                shadow_uv *= ( 1.f / uniform_buffer.shadow_size );
                float shadow_depth = texture ( sampler2D ( tex_shadows, sampler_linear ), shadow_uv ).x;

                float shadow_bias = 0.00005;
                float shadow_contribution = shadow_screen.z - shadow_bias > shadow_depth ? 1.f : 0.f;

                if ( shadow_screen.z > 1.f ) {
                    shadow_contribution = 0;
                }

                shadow += shadow_contribution;
            }
        }

        shadow /= ( pcf_radius * 2 + 1 ) * ( pcf_radius * 2 + 1 );

#else
        float shadow_bias = 0.00002;

        vec2 shadow_uv = shadow_texel + vec2 (-1.0, -1.0 );
        shadow_uv = clamp ( shadow_uv, light.shadow_tile.xy + vec2 ( 1 ), light.shadow_tile.xy + vec2 ( light.shadow_tile.z, light.shadow_tile.z ) - vec2 ( 1 ) );
        shadow_uv *= texel_size;
        shadow += shadow_gather ( shadow_uv, shadow_screen.z - shadow_bias );
        
        shadow_uv = ( shadow_texel + vec2 ( +1.0, -1.0 ) );
        shadow_uv = clamp ( shadow_uv, light.shadow_tile.xy + vec2 ( 1 ), light.shadow_tile.xy + vec2 ( light.shadow_tile.z, light.shadow_tile.z ) - vec2 ( 1 ) );
        shadow_uv *= texel_size;
        shadow += shadow_gather ( shadow_uv, shadow_screen.z - shadow_bias );
        
        shadow_uv = ( shadow_texel + vec2 ( -1.0, +1.0 ) );
        shadow_uv = clamp ( shadow_uv, light.shadow_tile.xy + vec2 ( 1 ), light.shadow_tile.xy + vec2 ( light.shadow_tile.z, light.shadow_tile.z ) - vec2 ( 1 ) );
        shadow_uv *= texel_size;
        shadow += shadow_gather ( shadow_uv, shadow_screen.z - shadow_bias );
        
        shadow_uv = ( shadow_texel + vec2 ( +1.0, +1.0 ) );
        shadow_uv = clamp ( shadow_uv, light.shadow_tile.xy + vec2 ( 1 ), light.shadow_tile.xy + vec2 ( light.shadow_tile.z, light.shadow_tile.z ) - vec2 ( 1 ) );
        shadow_uv *= texel_size;
        shadow += shadow_gather ( shadow_uv, shadow_screen.z - shadow_bias );

        shadow /= 16.0;
#endif

        float light_visibility = ( 1.f - shadow );

        vec3 view_light_pos = ( frame_uniforms.view_from_world * vec4 ( world_light_pos, 1 ) ).xyz;
        vec3 view_light_dir = normalize ( view_light_pos - view_geo_pos );

        vec3 surface = brdf ( view_light_dir, view_normal, normalize ( -view_geo_pos ), base_color, roughness, metalness );

        float nl = clamp ( dot ( view_light_dir, view_normal ), 0, 1 );
        float dist = distance ( view_geo_pos, view_light_pos );

        //irradiance += light_visibility * light_emissive * light_color * surface * nl / ( dist * dist );
        irradiance += light_visibility * light_emissive * light_color * surface * nl * light_falloff ( dist, light.radius );
    }

    float ambient_emissive = light_buffer.light_count > 0 ? 0.00 : 0.2;
    vec3 ambient_color = vec3 ( 1, 1, 1 );
    vec3 direct = irradiance + ambient_emissive * ambient_color * base_color;// + radiosity;

    imageStore ( img_color, ivec2 ( gl_GlobalInvocationID.xy ), vec4 ( direct, 1 ) );
}
