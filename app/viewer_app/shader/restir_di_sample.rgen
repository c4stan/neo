#version 460
#extension GL_NV_ray_tracing : require
#extension GL_EXT_scalar_block_layout : enable

#include <xs.glsl>

#include "common.glsl"
#include "restir_di_common.glsl"

layout ( location = 0 ) rayPayloadNV ray_payload_t ray_payload;
layout ( location = 1 ) rayPayloadNV ray_payload_t shadow_ray_payload;

layout ( binding = 0, set = xs_shader_binding_set_dispatch_m ) uniform accelerationStructureNV scene;

struct light_t {
    float pos[3];
    float radius;
    float color[3];
    float emissive;
    uint id;
};

layout ( binding = 2, set = xs_shader_binding_set_dispatch_m, scalar ) buffer light_array_t {
    uint light_count;
    uint _pad0[3];
    light_t data[];
} light_buffer;

layout ( binding = 3, set = xs_shader_binding_set_dispatch_m, scalar ) buffer reservoir_array_t {
    reservoir_t data[];
} reservoir_buffer;

layout ( binding = 4, set = xs_shader_binding_set_dispatch_m ) uniform texture2D tex_color;
layout ( binding = 5, set = xs_shader_binding_set_dispatch_m ) uniform texture2D tex_normal;
layout ( binding = 6, set = xs_shader_binding_set_dispatch_m ) uniform texture2D tex_material;
layout ( binding = 7, set = xs_shader_binding_set_dispatch_m ) uniform texture2D tex_depth;

layout ( binding = 8, set = xs_shader_binding_set_dispatch_m ) uniform sampler sampler_linear;

vec3 load_vec3 ( float[3] f32 ) {
    return vec3 ( f32[0], f32[1], f32[2] );
}

void trace_ray ( vec3 pos, vec3 dir ) {
    const uint trace_flags = gl_RayFlagsOpaqueEXT;
    const uint cull_mask = 0xff;
    const uint sbt_hit_idx = 0;
    const uint sbt_stride = 0;
    const uint sbt_miss_idx = 0;
    const float t_min = 0.001;
    const float t_max = 10000.0;
    const int payload_id = 0;
    traceNV (
        scene,
        trace_flags,
        cull_mask,
        sbt_hit_idx,
        sbt_stride,
        sbt_miss_idx,
        pos,
        t_min,
        dir,
        t_max,
        payload_id
    );
}

void trace_shadow_ray ( vec3 pos, vec3 dir ) {
#if 0
    const uint trace_flags = gl_RayFlagsOpaqueEXT | gl_RayFlagsTerminateOnFirstHitNV;
    const uint cull_mask = 0xff;
    const uint sbt_hit_idx = 0;
    const uint sbt_stride = 0;
    const uint sbt_miss_idx = 1;
    const float t_min = 0.001;
    const float t_max = 10000.0;
    const int payload_id = 1;
    traceNV (
        scene,
        trace_flags,
        cull_mask,
        sbt_hit_idx,
        sbt_stride,
        sbt_miss_idx,
        pos,
        t_min,
        dir,
        t_max,
        payload_id
    );
#else
    const uint trace_flags = gl_RayFlagsOpaqueEXT;
    const uint cull_mask = 0xff;
    const uint sbt_hit_idx = 0;
    const uint sbt_stride = 0;
    const uint sbt_miss_idx = 0;
    const float t_min = 0.001;
    const float t_max = 10000.0;
    const int payload_id = 1;
    traceNV (
        scene,
        trace_flags,
        cull_mask,
        sbt_hit_idx,
        sbt_stride,
        sbt_miss_idx,
        pos,
        t_min,
        dir,
        t_max,
        payload_id
    );
#endif
}

float light_p ( uint light_idx, vec3 world_pos, vec3 world_normal, vec3 base_color ) {
    light_t light = light_buffer.data[light_idx];
    vec3 light_pos = load_vec3 ( light.pos );
    vec3 light_color = load_vec3 ( light.color );
    vec3 light_dir = normalize ( light_pos - world_pos );
    float cos_theta = max ( 0, dot ( world_normal, light_dir ) );
    float d = distance ( light_pos, world_pos );
    float p_hat = length ( light.emissive * light_color * base_color * cos_theta / ( d * d * PI ) );
    return p_hat;
}

void main ( void ) {
    uvec2 screen_tex = uvec2 ( gl_LaunchIDNV.x, gl_LaunchSizeNV.y - gl_LaunchIDNV.y - 1 );
    vec2 screen_uv = vec2 ( screen_tex ) / gl_LaunchSizeNV.xy;
    rng_wang_state_t rng_state = rng_wang_init ( screen_tex );

    vec4 normal_rough_sample = texture ( sampler2D ( tex_normal, sampler_linear ), screen_uv );
    vec3 view_normal = normalize ( normal_rough_sample.xyz * 2 - 1 );
    float roughness = normal_rough_sample.w;
    vec4 color_metal_sample = texture ( sampler2D ( tex_color, sampler_linear ), screen_uv );
    vec3 base_color = color_metal_sample.xyz;
    float metalness = color_metal_sample.w;
    float depth = texture ( sampler2D ( tex_depth, sampler_linear ), screen_uv ).x;

    vec3 view_geo_pos = view_from_depth ( screen_uv, depth );
    vec3 world_geo_pos = ( frame_uniforms.world_from_view * vec4 ( view_geo_pos, 1.f ) ).xyz;
    vec3 world_normal = mat3 ( frame_uniforms.world_from_view ) * view_normal;

    uint light_count = light_buffer.light_count;
    uint light_pool = min ( light_count, 32 );
    float light_pdf = 1.0 / light_pool;
    reservoir_t reservoir = reservoir_init();
    
    if ( depth < 1 ) {
        for ( uint i = 0; i < light_pool; ++i ) {
            uint light_idx = int ( rng_wang ( rng_state ) * light_pool );
            float p_hat = light_p ( light_idx, world_geo_pos, world_normal, base_color );
            update_reservoir ( reservoir, light_idx, p_hat / light_pdf, 1, rng_state );
        }

        float p_hat = light_p ( reservoir.light, world_geo_pos, world_normal, base_color );
        update_reservoir_weight ( reservoir, p_hat );

        light_t light = light_buffer.data[reservoir.light];
        vec3 light_pos = load_vec3 ( light.pos );
        vec3 light_dir = normalize ( light_pos - world_geo_pos );
        trace_shadow_ray ( world_geo_pos + light_dir * 0.04, light_dir );
        if ( shadow_ray_payload.id != light.id ) {
            reservoir.weight = 0;
        }
    }

    uint reservoir_idx = screen_tex.x + screen_tex.y * frame_uniforms.resolution_u32.x;
    reservoir_buffer.data[reservoir_idx] = reservoir;
}
